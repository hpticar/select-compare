********************************************************************************
***  Script Code : S-LRE-ADDDISCOUNT
***  Description : Add Discount
***
***  Created By  : John Wheeler On 07/10/19 @ 18:53:36
***
***  Script Change Notes:
***
***
********************************************************************************
[&Assign LF01=0] // amount to be paid
[&Assign LF02=0] // discount amount
[&Assign LF03=0] // total remaining
[&Assign LF04=""] // reson for discount
[&Assign LF05=""] // reson details
[&Assign LF06=""] // LRE schedules
[&Assign LF07=date]
[&Assign LF08="loop"] // dowhile loop trigger
[&Assign LF09=""] // instalment plan status
[&Assign LF10=0] // active instalment plan group instance
[&Assign LF11=""] // LRE password
[&Assign LF101=""] // schedule name
[&Assign LF133=""] // schedule comments
[&Assign LF151=""] // schedule History note
[&Assign LF152=""] // timedate stamp
[&Assign LF498=""] // schedule name
[&Assign LF600=""] // schedule code
[&SelectEntity type="ADMI",ref="myAdmi"]

[&If LRE06>0]
   [&Message "A discount has already been applied to this LRE, you cannot apply another.",title=" Error applying discount"]
[&Else]
   [&Call S-LRE-GETINSTALMENTGROUPINDEX,output=LF09,output=LF10]
   [&If LF09="active"]
      [&Message "You are about to apply a discount to a claim that has an active instalment plan.  Please review for any action required at Realex.",title="Warning"]
   [&EndIf]
   [&DoWhile LF08="loop"]
      [&Assign LF01=0]
      [&DoWhile LF01<1]
         [&Assign LF01='How much of the total LRE does the policyholder now need to pay?'(number)]
         [&If LF01<1]
            [&Message "You must enter an amount greater than zero",title=" Error applying discount"]
         [&EndIf]
      [&EndDoWhile]   
      [&Assign LF02=DAT01-LF01]
      [&If LF02<0]
         [&Message "This discount would result in a negative outstanding and cannot be applied",title=" Error applying discount"]
      [&Else]   
         [&If LF02=0]
            [&Message "A discount of zero cannot be applied",title=" Error applying discount"]
         [&Else]
            [&Assign LF03=LRE04-LF02] 
            [&If LF03<0]
               [&Message "This discount would result in a negative outstanding and cannot be applied",title=" Error applying discount"]
            [&Else]
               [&If LF03=0]
                  [&Assign LF11='This discount will result in zero outstanding. Please enter password:'(password, no-cancel)]
                  [&If LF11=myAdmi.LRE03]
                     [&Assign LF08="stop loop"]
                     [&Assign LRE07="Paid in Full"]
                     [&If LF09="active"]
                        [&Group code="GINS",action="setindex",index=LF10]
                        [&Assign INS01="Complete"]
                        [&Assign INS06=0]
                        [&Assign LF09="Complete"]  
                     [&EndIf]
                  [&Else]
                     [&Message "Wrong password",title=" Error!"]    
                  [&EndIf]
               [&Else]
                  [&Assign LF08="stop loop"]      
               [&EndIf]
            [&EndIf]   
         [&EndIf]
      [&EndIf]   
   [&EndDoWhile] 
   [&Assign LRE04=LF03]
   [&Assign LRE06=LF02]
   [&UserInput prompt="Why have you applied a discount to this LRE?",choices="EOD:Aged Debt:Goodwill:Avoid Formal EOD:Other",result=LF04,no-cancel]
   [&Assign DAT06=LF04]
   [&If LF04="Goodwill" &Or LF04="Other"]
      [&DoWhile LF05=""]
         [&UserInput prompt="Please enter details of why discount has been applied",type="text",result=LF05,no-cancel]
         [&If LF05=""]
            [&Message "Reason cannot be blank",title=" Error"]
         [&EndIf]
      [&EndDoWhile]
      [&Assign DAT07=LF05]
   [&Else]
      [&Assign DAT07=""] // clear just in case
   [&EndIf]
   [&Assign DAT08=US01]
   [&If LF09="active"]
      [&Group code="GINS",action="setindex",index=LF10]
      [&Call S-INS-INSTALMENTS-FINALPAYMENT,input="discount"]
   [&EndIf]
   [&With owner]
      [&History "LRE discount applied - Â£"+LF02,category="LRE"]
      [&If LF03=0]
         [&ForEach ad,AD11 &Contains"LRE"]
            [&Assign LF101=AD01] // Put ADs into LF as &History sometimes loses correct AD
            [&Assign LF133="<CR>"+AD33]
            [&History "SCHEDULE completed: "+LF101;LF133,user-data1="ScheduleManagement",category="Schedule Management"]
            [&Kill code=AD05 ,noprompt]
         [&EndFor]
         [&Assign LF498="LRE Paid in Full - Remove block at TCAS"]
         [&Assign LF151=US02+" created "+LF498+", due-date="+LF07+", priority=Normal, handler=LRE"]
         [&Call S-SCHEDULE-CODES,output=LF600]
         [&Schedule S-WF-REMINDER,LF498,date-due=LF07,category="LRE",fee-earner="LRE",schedule-code="S-WF-REMINDER-"+LF600]
         [&Call S-DATETIME-STAMP,output=LF152]
         [&Assign LF152="<CR>"+LF152+" "+LF151]
         [&History "SCHEDULE created: "+LF498;LF152,user-data1="ScheduleManagement",category="Schedule Management"]  
      [&EndIf]
      [&Call S-REC-ADJUST,input="Policyholder LRE Recovery Reserve",input=PHR18,input="LRE Recovery",input=PHR18-LF02]
   [&EndWith]             
[&EndIf]

********************************************************************************
***   End of Script
********************************************************************************