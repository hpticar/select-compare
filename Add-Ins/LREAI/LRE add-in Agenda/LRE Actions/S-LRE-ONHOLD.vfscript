********************************************************************************
***  Script Code : S-LRE-ONHOLD
***  Description : Put LRE on Hold
***
***  Created By  : Joe Obeney (Dev Acc) On 08/10/19 @ 17:46:47
***
***  Script Change Notes:
***
***
********************************************************************************
[&Assign LF02=""]// Active instalment result
[&Assign LF03=""]// Group index of active instalment plan
[&Assign LF07=""]// LRE status of group index
[&Assign LF08=""]// Reason LRE put on hold
[&Assign LF10=""]// marker for active installment plan
[&Assign LF12=""]// LRE on hold 'Other' description
[&Assign LF13=0]// due date for LRE on hold schedule
[&Assign LF600=""]

[&Call S-LRE-GETINSTALMENTGROUPINDEX ,output=LF02,output=LF03]
[&If LF02="Active"]
   [&If 'There is an active instalment plan in place.  Do you wish to proceed with putting the LRE on hold?'(Question) = "Yes"]
      [&Message "You must cancel the instalment plan on Realex!"]
      [&Assign INS01="On Hold"]
      // Delete LRE Schedules
      [&SelectEntity type="ADMI",ref="myAdmi"]
      [&Assign LF08=myAdmi.LRE02]
      [&With owner]
         [&ForEach ad,AD11 &Contains"LRE"]
            [&Assign LF101=AD01] // Put ADs into LF as &History sometimes loses correct AD
            [&Assign LF133="<CR>"+AD33]
            [&History "SCHEDULE completed: "+LF101;LF133,user-data1="ScheduleManagement",category="Schedule Management"]
            [&Kill code=AD05,noprompt]
         [&EndFor]
      [&EndWith]
   [&Else]
      [&Quit]
   [&EndIf]
[&EndIf]

[&Assign LRE07="On Hold"]
[&UserInput prompt="Why is LRE being put on hold",choices="EOD:Awaiting Evidence:Other",no-cancel,result=LF08]
[&Assign DAT02=LF08]
[&If LF08="Other"]
   [&UserInput prompt="Reason for placing LRE on hold", result=LF11,no-cancel]
   [&Assign DAT03=LF11]
   [&Assign LF12=LF08+" - "+ LF11]
[&Else]
   [&Assign LF12=LF08] 
[&EndIf] 
   
[&If LF08="EOD"]
   [&Assign LF13=date+6(weeks)] 
[&Else]
   [&Assign LF13=date+1(weeks)]
[&EndIf]
  
[&With owner]
   [&Call S-SCHEDULE-CODES,output=LF600]
   [&Assign LF498="LRE put on hold - "+LF12]
   [&Assign LF151=US02+" created "+LF498+",due-date="+LF13+", priority=Normal, handler=LRE"]
   [&Schedule SCH-S-LRE-ONHOLD,LF498,date-due=LF13,priority="normal",fee-earner="LRE",category="LRE",schedule-code="SCH-S-LRE-ONHOLD-"+LF600]
   [&History  LF498,category="Policyholder;Claim Notes;Documents and Notes;LRE"]
   [&Call S-DATETIME-STAMP,output=LF152]
   [&Assign LF152="<CR>"+LF152+" "+LF151]
   [&History "SCHEDULE created: "+LF498;LF152,user-data1="ScheduleManagement",category="Schedule Management"]
[&EndWith]

[&Display LRE]
********************************************************************************
***   End of Script
********************************************************************************