********************************************************************************
***  Script: S-OVD-LOCATION                                                  ***
***  Called By:                                                              ***
***  Calls:                                                                  ***
***  Created By: HP On 03/07/13                                              ***
***                                                                          ***
***  Amended By: AR 12/08/2014                                               ***
***              Added checks for each possible location - so data can be    ***
***              populated in OVL                                            ***
***                                                                          ***
********************************************************************************
[&Assign LF01=""] VLO01
[&Assign LF02=""] VLO02
[&Assign LF03=""] VLO03
[&Assign LF04=""] VLO04
[&Assign LF05=""] VLO05
[&Assign LF06=""] VLO06
[&Assign LF07=""] VLO07
[&Assign LF08=""] VLO08
[&Assign LF09=""] VLO09
[&Assign LF10=""] VLO10
[&Assign LF11=OVD19] dropdown option
[&Assign LF12=0] // GREP count
[&Assign LF13=""] // all addresses found
[&Assign LF14=""] // repaier full address
[&Assign LF15=0] // number of Repairs found
[&Assign LF16=""] // display grid response
[&Assign LF17=TF500] // DMG20 vaule onview
[&Assign LF18=""] // tests for blank addresses
[&Assign LF19=""] // Overwrite question message
[&Assign LF20="No"] // mp repairer found
[&Assign LF21="no"] // overwrite question response
[&Assign LF22=EN56] current OP
[&Assign LF23=""] // LF to check if user quits process

// DEPENDING UPON WHAT IS selected, POPULATE THE VLO screen
// OVD19 CAN BE "OP Address,Driver's Address,Repairer,Other,Unknown"

//NO location selected
[&If LF11="Select..."]
  [&Message "Please select a location for the vehicle...",title="Missing Vehicle Location"]
  [&Quit]
[&EndIf]

// OTHER PARTY'S ADDRESS
[&If LF11="OP Address"]
   [&SelectRelation relation-field=OPD01,ref="myOP"]
   [&With myOP]
      [&If EN06="INDI"]
         // OP IS PRIV
         [&Assign LF01=""]
         [&Assign LF02=NAM04]
         [&Assign LF03=NAM05]
         [&Assign LF04=NAM06]
         [&Assign LF05=NAM07]
         [&Assign LF06=NAM08]
         [&Assign LF07=NAM09]
         [&Assign LF08=NAM10]
         [&Assign LF09=CON01]
         [&Assign LF10=CON03]
      [&Else]
         // OP IS COMM
         [&Assign LF01=OFF01]
         [&Assign LF02=OFF04]
         [&Assign LF03=OFF03]
         [&Assign LF04=OFF06]
         [&Assign LF05=OFF07]
         [&Assign LF06=OFF08]
         [&Assign LF07=OFF09]
         [&Assign LF08=OFF10]
         [&Assign LF09=CON02]
         [&Assign LF10=CON01]
      [&EndIf]
   [&EndWith]
   [&Assign LF18=LF01+LF02+LF03+LF04+LF05+LF06+LF07+LF08]
   [&If LF18=""]
      [&Message "No Address for OP found. Please add address and try again.",title="Missing Address"]
      [&Assign OVD19=LF17]
      [&Quit]
   [&EndIf]
[&EndIf]

// DRIVER'S ADDRESS
[&If LF11="Driver's Address"]
  // CHECK IF DRIVER IS SELECTED
   [&If ODR02=""]
      [&If prompt("Vehicle address set as driver's address but no driver is on file for this OP. Would you like to enter Driver details?")(question)="Yes"]
         [&Show ODR,title="Please enter the Driver Details",no-cancel,no-help]
         [&If ODR02=""]
            [&Assign OVD19=LF17]
            [&Quit no-status]
         [&EndIf]
      [&Else]
         [&Assign OVD19=LF17]
         [&Quit no-status]
      [&EndIf]
   [&EndIf]
   [&SelectRelation relation-field=ODR01,ref="myOPDriver"]
   [&With myOPDriver]
      [&Assign LF01=""]
      [&Assign LF02=NAM04]
      [&Assign LF03=NAM05]
      [&Assign LF04=NAM06]
      [&Assign LF05=NAM07]
      [&Assign LF06=NAM08]
      [&Assign LF07=NAM09]
      [&Assign LF08=NAM10]
      [&Assign LF09=CON01]
      [&Assign LF10=CON03]
   [&EndWith]
   [&Assign LF18=LF01+LF02+LF03+LF04+LF05+LF06+LF07+LF08]
   [&If LF18=""]
      [&Message "No Address for Driver found. Please add address and re-try.",title="Missing Address"]
      [&Assign OVD19=LF17]
      [&Quit]
   [&EndIf]
[&EndIf]


[&If LF11="Repairer"]
  [&SelectRelation type="OTHERPARTY_FILE",ref="myFILE"]
  [&With myFILE]
     [&Count type="group",code="GREP",result=LF12]
     [&If LF12=1] // if only 1 repaier
        [&If REP21="Other Party"]
           [&SelectRelation relation-field="REP22",ref="forOP"]
           [&If forOP.EN56=LF22] if it's this OP
              [&Show REP,no-cancel,no-help,no-navigate]
              [&Assign LF20="Yes"]
           [&Else]
              [&Assign LF20="no"] // No repaier for OP
           [&EndIf]   
        [&Else]
           [&Assign LF20="no"] // No repaier for OP
        [&EndIf]
     [&Else]
        [&Grid action="delete"]
        [&Grid action="create"]
        [&Grid action="create-col",name="Repairer",type="char",format="x(24)"]                1 Repaier name
        [&Grid action="create-col",name="address",type="char",format="x(160)"]                2 full address
        [&Grid action="create-col",name="group index",type="integer",key,hidden]              3 group index
        [&Grid action="create-col",name="Selected",type="char",format="x(3)",hidden,selected] 4
        [&Assign LF15=0]
        [&ForEach GREP,REP21="Other Party" &And REP01<>"",noincludeall]
           [&SelectRelation relation-field="REP22",ref="forOP"]
           [&If forOP.EN56=LF22]
              [&Assign LF14=REP04] // repairer full address
              [&If LF13 &Contains LF14]
                 do nothing already have a row for this address
              [&Else]
                 [&Grid action="create-row"]
                 [&Assign GR01=REP02]
                 [&Assign GR02=LF14]
                 [&Group code="GREP",action="getindex",result=GR03]
                 [&Assign LF13=LF13+" "+LF14]
                 [&Assign LF15=LF15+1]      
              [&EndIf]
           [&EndIf]
        [&EndFor]
        [&If LF15=0] 
           [&Assign LF20="no"] // no repairer found
        [&Else]
           [&If LF15=1]
              [&Group code="GREP",action="setindex",index=GR03]
              [&Show REP,no-cancel,no-help,no-navigate]
              [&Assign LF20="yes"]
           [&Else]      
              [&Grid action="display",title="Please select correct repairer that vehicle is located.",rows=LF15,button1="Cancel",button2="OK",response=LF16,default="2"]     
              [&If LF16="button2" &Or LF16="go"]
                 [&Group code="GREP",action="setindex",index=GR03] // set index to selected repaier
                 [&Assign LF20="Yes"] // repairer found
              [&Else]
                 [&Assign LF23=LF17] // user quit can't assign OVD19 untill out the with
              [&EndIf]
           [&EndIf]
        [&EndIf] 
  
        [&If LF20="No"] haven't found a repairer
           [&If prompt("No repairer on File for this OP. Would you like to enter repairer details?")(question)="Yes"]
              [&SelectEntity int-code=LF22,ref="myself"]
              [&Select GREP:new]
              [&Assign REP21="Other Party"]
              [&CreateRelation relation-field=REP22,to-ref="myself"]
              [&Show REP,no-cancel,no-help,no-navigate]
          [&Else]
             [&Assign LF23=LF17] // user quit can't assign OVD19 untill out the with
          [&EndIf]
        [&EndIf]
     [&EndIf]
  
      [&SelectRelation relation-field="REP18",ref="myRepoffice"]
      [&If status(selectrelation)=""]
         [&With myRepoffice]
            [&Assign LF01=OFF01]
            [&Assign LF02=OFF04]
            [&Assign LF03=OFF03]
            [&Assign LF04=OFF06]
            [&Assign LF05=OFF07]
            [&Assign LF06=OFF08]
            [&Assign LF07=OFF09]
            [&Assign LF08=OFF10]
            [&Assign LF09=CON02]
            [&Assign LF10=CON01]
         [&EndWith]
         [&Assign LF18=LF01+LF02+LF03+LF04+LF05+LF06+LF07+LF08]
         [&If LF18=""]
            [&Message "No Address for Repaier found. Please add address and try again.",title="Missing Address"]
            [&Assign LF23=LF17] // no address but in with so set marker
         [&EndIf]
      [&Else]
         [&Message "Unable to select repairer, if problem continues please raise a ticket to IT.",title="Error selecting address"]
      [&EndIf]  
  [&EndWith]
  [&If LF23=LF17] // if user quits prompt
     [&Assign OVD19=LF17]
     [&Quit]
  [&EndIf]
[&EndIf]

[&Assign LF18=""]
[&Assign LF18=OVL01+OVL02+OVL03+OVL04+OVL05+OVL06+OVL07+OVL08+OVL09+OVL10]
[&If LF18<>""]
   [&If LF11="Other"]
      [&Assign LF19="Do you want to clear and enter a new location?"]
   [&Else]
      [&Assign LF19="Do you want to overwrite with "+LF11+"?"]
   [&EndIf]   
   [&Assign LF21=prompt("Vehicle address already contains data. "+LF19)(question)]
   [&If LF21="no"]
      [&Assign OVD19=LF17]
      [&Quit]
   [&EndIf]
[&Else]
   [&Assign LF21="yes"]
[&EndIf]

[&If LF21="yes"]
   [&If LF11="Other"]
      [&Show OVL,title="Please ensure you set the vehicle location",no-cancel,no-help]
   [&Else]
      [&Assign OVL01=LF01]
      [&Assign OVL02=LF02]
      [&Assign OVL03=LF03]
      [&Assign OVL04=LF04]
      [&Assign OVL05=LF05]
      [&Assign OVL06=LF06]
      [&Assign OVL07=LF07]
      [&Assign OVL08=LF08]
      [&Assign OVL09=LF09]
      [&Assign OVL10=LF10]
   [&EndIf]
[&Else]
   [&Assign OVD19=LF17]
[&EndIf] 
