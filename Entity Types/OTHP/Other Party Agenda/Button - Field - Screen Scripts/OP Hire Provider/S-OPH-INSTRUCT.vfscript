********************************************************************************
***  Script Code : S-OPH-INSTRUCT
***  Description : Instruct Selected Hire Provider
***
***  Created By  : Hrvoje Pticar On 06/08/18 @ 13:33:19
***
***  Script Change Notes:
***
***
********************************************************************************
[&Assign LF02="Default"]
[&Assign LF56=EN56:text:replace(".00","")]
[&Assign LF56=LF56:replace(",","")]
[&Assign LF57=OPD01]
[&SelectEntity type="ADMI",ref="myADMI"]
[&With myADMI]
   [&Assign LF102=CHR03]  //list of CH providers - "<EN56>,<XML-valid company code>|"
   [&Assign LF104=SYS09]
   [&Assign LF105=SYS03]
[&EndWith]

[&Sequence name="Supplier Instruction Sequence",currentvalue=LF01,increment]

[&SelectRelation relation-field=OPH01,ref="myHIRER"]
[&If status(selectrelation)=""]
   [&Assign LF101=myHirer.EN56:whole:text]
   [&Assign LF102=myAdmi.CHR03]  //list of CH providers - "<EN56>,<XML-valid company code>|"
   [&DoWhile LF102<>""]
      [&Assign LF103=LF102:word(first=1,number=1,delimiter="|")] //grab first set
      [&If LF103:word(first=1,number=1,delimiter=",")=LF101] //if the EN56 matches...
         [&Assign LF02=LF103:word(first=2,number=1,delimiter=",")] //...then grab the XML company code
      [&EndIf]
      [&Assign LF102=LF102:word(first=2,number=99,delimiter="|")] //go to next set
   [&EndDoWhile]
   [&If LF02<>"Default"] //if we found a valid CH company
      [&SelectRelation type="OTHERPARTY_FILE",ref="myFILE"]
      [&With myFILE]
         [&Call S-HIR-CREATE-INSTR-XML,input=LF56,input=LF01,input=LF02,output=LF03,output=LF04] // Returns success indicator + path to XML file
      [&EndWith]      

      [&If LF03="Failed"]
         [&Message "Instruction has not been sent."]
         [&Quit]
      [&Else]
         [&Assign LF05="c:\temp\vf\"+US01+"_HireInstructionResponse.txt"]
         // Usage: InstructHire 1.Environment 2.ProviderID 3.SEND 5.ResponseFilePath 5.XmlFilePath 6.PartyID
         [&OSCommand LF104+"\InstructHire.exe "+LF105+" "+LF02+" send "+LF05+" "+LF04+" "+LF56]
         [&Import filename=LF05]
            [&Assign LF06=IF01]
         [&EndImport]
         [&If LF06:word(first=1,number=1)="226"]
            [&With myFILE]
               [&History "Hire Instruction for "+LF57+" sent to "+LF02+".",read=LF04]
            [&EndWith]
            [&Assign OPH07="Sent"]
            [&Assign OPH22=date]
            [&Message "Hire Instruction sent.",title="Information"]
         [&Else]
            [&Message LF06,title="Error"]
         [&EndIf]
      [&EndIf]
   [&Else]
      [&Message "Aborted - Selected hire company does not support electronic instructions.",title="System Error"]
   [&EndIf]
[&Else]
   [&Message "Aborted - No hire company selected on Hire Provider screen.",title="System Error"]
   [&Quit]
[&EndIf]

********************************************************************************
***   End of Script
********************************************************************************