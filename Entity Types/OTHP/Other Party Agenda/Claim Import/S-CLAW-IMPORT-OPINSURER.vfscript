********************************************************************************
***  Script Code : S-CLAW-IMPORT-OPINSURER
***  Description : CLAW Import OP Insurer
***
***  Created By  : Hrvoje Pticar On 29/07/15 @ 16:20:00
***
***  Script Change Notes:
***
***
********************************************************************************

[&Assign LF85=GF116+"\importINSURERS.txt"]
[&OSCommand "dir "+GF116+"\INSURERS\*.XML /B >"+LF85]

[&Assign LF91=0] number of imported insurers
[&Assign LF92=""] indicator for primary insurer

[&Import filename=LF85]
   [&Assign LF91=LF91+1]
   [&Assign LF88=GF116+"\INSURERS\"+IF01]
   [&Assign XL01(file)=LF88]

   [&Select GOPN:new]
   [&Out GF113:date+"|"+time(seconds)+"|INFO|OTHP.S-CLAW-IMPORT-OPINSURER|Claim GUID: "+GF112+"|OP: "+EN02+"|Importing "+IF01+"...",append]
   
   [&Select xl,type=XL01,element=InsurerType,first,parent]
   [&Assign LF01=0]
   [&Assign LF01=XL01(element=HavenIntCode,child)]
   [&Assign LF01=LF01:number]

   [&Assign LF03=XL01(element=reference,child)]
   [&Assign LF04=XL01(element=PrimaryInsurer,child)]
   [&If LF04="Yes"]
      [&Assign LF92="Primary"]
   [&EndIf]
   [&Assign LF05=XL01(element=ContactName,child)]
   
   [&Assign LF61=""]
   [&Assign LF62=""]
   [&Assign LF63=""]
   [&Assign LF64=""]
   [&Assign LF65=""]
   [&Assign LF66=""]
   
   // if no int-code provided, try retrieving details
   [&If LF01=0]
      [&Assign LF02=XL01(element=name,child)]

      [&Select xl,type=XL01,element=InsurerType.Comms.Method,first,parent]
      [&DoWhile status(select)=""]
         [&If XL01(element=ContactDescription,child)="Phone - Day"]
            [&Assign LF61=XL01(element=NumberAddress,child)]
         [&Else]
            [&If XL01(element=ContactDescription,child)="Phone - Evening"]
               [&Assign LF62=XL01(element=NumberAddress,child)]
            [&Else]
               [&If XL01(element=ContactDescription,child)="Phone - Mobile"]
                  [&Assign LF63=XL01(element=NumberAddress,child)]
               [&Else]
                  [&If XL01(element=ContactDescription,child)="Fax"]
                     [&Assign LF64=XL01(element=NumberAddress,child)]
                  [&Else]
                     [&If XL01(element=ContactDescription,child)="EMail"]
                        [&Assign LF65=XL01(element=NumberAddress,child)]
                     [&EndIf]
                  [&EndIf]
               [&EndIf]
            [&EndIf]
         [&EndIf]
         [&If XL01(element=Preferred,child):text="Yes"]
            [&Assign LF66=XL01(element=NumberAddress,child)]
         [&EndIf]
         [&Select xl,type=XL01,element=InsurerType.Comms.Method,next,parent]
      [&EndDoWhile]
   [&EndIf]
   
   // insurer name should be at least 2 characters long
   [&If length(LF02)<2]
      [&Out GF113:date+"|"+time(seconds)+"|ERROR|OTHP.S-CLAW-IMPORT-OPINSURER|Claim GUID: "+GF112+"|OP: "+EN02+"|Invalid insurer name ("+LF02+"). Skip.",append]
   [&Else]
      // if no int-code was provided, create COMM and OFFI based on details provided
      [&If LF01=0]
         // first remove any lf/cr
         [&Assign TF01=LF02:replace("
","")]
         [&Finder runquery="importMatchInsurer",output="grid",autorun]
         [&Assign LF301=0]
         [&ForEach GR]
            [&SelectEntity type="COMM",int-code=GR04,ref="myINSURER"]
            [&Assign LF301=LF301+1]
         [&EndFor]
         [&If LF301>1]
            [&Out GF113:date+"|"+time(seconds)+"|ERROR|OTHP.S-CLAW-IMPORT-OPINSURER|Claim GUID: "+GF112+"|OP: "+EN02+"|Multiple Insurer Matches!! ("+LF02+").",append]
         [&EndIf]
         [&If LF301<1]
            [&Select xl,type=XL01,element=InsurerType.Address,first,parent]
            [&Assign LF20=XL01(element=TypeInd,child)]
            [&Assign LF21=XL01(element=HavenIntCode,child)]
            [&Assign LF22=XL01(element=Address1,child)]
            [&Assign LF23=XL01(element=Address2,child)]
            [&Assign LF24=XL01(element=Address3,child)]
            [&Assign LF25=XL01(element=Address4,child)]
            [&Assign LF26=XL01(element=postcode,child)]
         
            [&CreateEntity type="COMM",ref="myINSURER",noscript]
            [&With myINSURER]
               [&Assign DIN06=LF02]
               [&Assign DIN17=LF22]
               [&Assign DIN18=LF23]
               [&Assign DIN19=LF24]
               [&Assign DIN20=LF25]
               [&Assign DIN21=LF26]
               [&Assign DIN22=LF61]
               [&Assign DIN24=LF65]
               [&Call S-DIN-HIDE]
               [&Call create-entity]
               [&CreateRole type="OPINSURER",ref="myINSURERrole"]
            [&EndWith]
         [&EndIf]
      [&Else]
         // if insurer int-code exists, just select the entity
         [&SelectEntity type="COMM",int-code=LF01,ref="myINSURER"]
      [&EndIf]
      
      [&CreateRelation relation-field=OPN01,to-ref="myINSURER"]
   
      [&With myINSURER]
         [&SelectRelation type="PRINCIPALOFFI_OFFI",ref="myOFFI"]
      [&EndWith]
      
      [&Select xl,type=XL01,element=InsurerType.Insurance,first,parent]
      [&Assign LF70=XL01(element=PolicyNumber,child)]
      [&Assign LF71=XL01(element=PolicyCover,child)]
      [&Assign LF72=XL01(element=PolicyExcess,child)]
      
      [&CreateRelation relation-field=OPN18,to-ref="myOFFI"]
      [&Assign OPN20=LF03]
      [&Assign OPN22=LF70]
      [&Assign OPN23=LF71]
      [&Assign OPN24=LF72]
      [&Assign OPN17=LF04]
      
      [&Assign OPN08=LF05]
      [&If LF66<>""]
         [&Assign OPN09=LF66]
      [&Else]
         [&Assign OPN09=LF61]
      [&EndIf]
      [&If LF62<>""]
         [&Assign OPN13=LF62]
      [&EndIf]
      [&If LF63<>""]
         [&Assign OPN14=LF63]
      [&EndIf]
      [&If LF64<>""]
         [&Assign OPN15=LF64]
      [&EndIf]
      [&If LF65<>""]
         [&Assign OPN10=LF65]
      [&EndIf]
   [&EndIf]
[&EndImport]

[&If LF91>0]
   [&If LF92<>"Primary"]
      [&Out GF113:date+"|"+time(seconds)+"|WARNING|OTHP.S-CLAW-IMPORT-OPINSURER|Claim GUID: "+GF112+"|OP: "+EN02+"|NO PRIMARY INSURER SET!",append]
   [&EndIf]
[&EndIf]

********************************************************************************
***   End of Script
********************************************************************************
